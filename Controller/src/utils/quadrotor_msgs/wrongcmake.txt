cmake_minimum_required(VERSION 3.8)
project(quadrotor_msgs)

# Default to C++14
if(NOT CMAKE_CXX_STANDARD)
  set(CMAKE_CXX_STANDARD 14)
endif()

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
  add_compile_options(-Wall -Wextra -Wpedantic)
endif()

find_package(ament_cmake REQUIRED)
find_package(builtin_interfaces REQUIRED)
find_package(geometry_msgs REQUIRED)
find_package(nav_msgs REQUIRED)
find_package(rosidl_default_generators REQUIRED)
find_package(rosidl_typesupport_c REQUIRED)
find_package(std_msgs REQUIRED)

set(msg_files
  "msg/AuxCommand.msg"
  "msg/Corrections.msg"
  "msg/Gains.msg"
  "msg/OutputData.msg"
  "msg/PositionCommand.msg"
  "msg/PPROutputData.msg"
  "msg/Serial.msg"
  "msg/SO3Command.msg"
  "msg/StatusData.msg"
  "msg/TRPYCommand.msg"
  "msg/Odometry.msg"
  "msg/PolynomialTrajectory.msg"
  "msg/LQRTrajectory.msg"
)

set(srv_files
  "srv/SetTakeoffLand.srv"
)

rosidl_generate_interfaces(${PROJECT_NAME}
  ${msg_files}
  ${srv_files}
  DEPENDENCIES builtin_interfaces geometry_msgs nav_msgs std_msgs
)

ament_export_dependencies(rosidl_default_runtime)

# For encode/decode libraries
find_package(Eigen3 REQUIRED)

include_directories(
  include
  ${EIGEN3_INCLUDE_DIR}
)

add_library(decode_msgs src/decode_msgs.cpp)
add_library(encode_msgs src/encode_msgs.cpp)

# Make sure the libraries depend on the generated messages
rosidl_target_interfaces(decode_msgs ${PROJECT_NAME} "rosidl_typesupport_cpp")
rosidl_target_interfaces(encode_msgs ${PROJECT_NAME} "rosidl_typesupport_cpp")

target_link_libraries(decode_msgs encode_msgs)

ament_target_dependencies(decode_msgs
  "geometry_msgs"
  "nav_msgs"
  "rosidl_default_runtime"
  "std_msgs"
)

ament_target_dependencies(encode_msgs
  "geometry_msgs"
  "nav_msgs"
  "rosidl_default_runtime"
  "std_msgs"
)

# Install libraries
install(
  TARGETS decode_msgs encode_msgs
  EXPORT decode_encode_msgs
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin
)

install(
  EXPORT decode_encode_msgs
  FILE decode_encode_msgs.cmake
  DESTINATION share/${PROJECT_NAME}/cmake
)

# Install headers
install(
  DIRECTORY include/
  DESTINATION include
)

# Install cmake files
install(
  DIRECTORY cmake/
  DESTINATION share/${PROJECT_NAME}/cmake
)

ament_export_include_directories(include)
ament_export_libraries(decode_msgs encode_msgs)
ament_export_dependencies(rosidl_default_runtime)

ament_package()